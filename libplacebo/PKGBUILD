# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libplacebo
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-libplacebo")
pkgver=6.338.1
pkgrel=1
pkgdesc="Reusable library for GPU-accelerated video/image rendering primitives (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'clang64' 'clangarm64')
url="https://code.videolan.org/videolan/libplacebo"
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-python-jinja"
  "${MINGW_PACKAGE_PREFIX}-fast_float"
  "${MINGW_PACKAGE_PREFIX}-vulkan-headers"
)
license=('LGPL2.1')
source=(${url}/-/archive/v${pkgver}/${_realname}-v${pkgver}.tar.gz)
sha256sums=('f748bf9385f4c228e1379d7d1bed13581176bfdc54eb99f4abe22e649f8dc93f')

build() {
  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --buildtype=release  \
      --default-library=static \
      -Ddemos=false \
      -Dtests=false \
      -Dopengl=disabled \
      -Dgl-proc-addr=disabled \
      -Dd3d11=disabled \
      -Dlcms=disabled \
      -Ddovi=disabled \
      -Dlibdovi=disabled \
      -Dglslang=disabled \
      -Dshaderc=disabled \
      -Dvulkan=disabled \
      "build-${MSYSTEM}" \
      "${srcdir}/${_realname}-v${pkgver}"

  meson compile -C "build-${MSYSTEM}"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  meson install --destdir "${pkgdir}"

  sed -s "s|$(cygpath -am ${MINGW_PREFIX})|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libplacebo.pc
}