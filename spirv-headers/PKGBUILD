# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>
# Contributor: Biswapriyo Nath <nathbappai@gmail.com>

_realname=spirv-headers
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
epoch=2
pkgver=1.3.261.1
pkgrel=1
pkgdesc='Machine-readable files for the SPIR-V Registry (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'clang64' 'clangarm64')
url='https://www.khronos.org/registry/spir-v/'
license=('spdx:MIT')
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
)
source=("${_realname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/SPIRV-Headers/archive/sdk-${pkgver}.tar.gz")
sha256sums=('32b4c6ae6a2fa9b56c2c17233c8056da47e331f76e117729925825ea3e77a739')

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe -B "build-${MSYSTEM}" -G Ninja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${srcdir}/SPIRV-Headers-sdk-${pkgver}"

  ${MINGW_PREFIX}/bin/cmake.exe --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install "build-${MSYSTEM}"

  install -Dm644 ${srcdir}/SPIRV-Headers-sdk-${pkgver}/README.md ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md
  install -Dm644 ${srcdir}/SPIRV-Headers-sdk-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}